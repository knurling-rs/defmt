/* exhaustively search for these symbols */
EXTERN(_defmt_acquire);
EXTERN(_defmt_release);
EXTERN(_defmt_timestamp);

SECTIONS
{
  .defmt (INFO) :
  {
    /* this section starts at address 1 */
    /* we use this fact when extract file locations from the DWARF information.
       symbols that appear in source code but won't make it to the final program
       (e.g. `if false { error!("foo")}`) will be given an address of 0 in DWARF
       to differentiate those "ghost" symbols from symbols that will be used, we
       have the latter start at address 1
    */
    . = 1;

    /* Format implementations for primitives like u8 */
    *(.defmt.prim.*);

    /* ERROR logging level */
    _defmt_error_start = .;
    *(.defmt.error.*);
    _defmt_error_end = .;

    /* WARN logging level */
    _defmt_warn_start = .;
    *(.defmt.warn.*);
    _defmt_warn_end = .;

    /* INFO logging level */
    _defmt_info_start = .;
    *(.defmt.info.*);
    _defmt_info_end = .;

    /* DEBUG logging level */
    _defmt_debug_start = .;
    *(.defmt.debug.*);
    _defmt_debug_end = .;

    /* TRACE logging level */
    _defmt_trace_start = .;
    *(.defmt.trace.*);
    _defmt_trace_end = .;

    /* Format/write! strings */
    *(.defmt.fmt.*);

    /* User interned strings (Str) */
    *(.defmt.str.*);

    /* $DEFMT_VERSION may contain special chars, so we quote the symbol name */
    /* Note that the quotes actually become part of the symbol name though! */
    "_defmt_version_ = $DEFMT_VERSION" = 1;
  }
}

ASSERT(SIZEOF(.defmt) < 16384, ".defmt section cannot contain more than (1<<14) interned strings");
